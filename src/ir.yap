

// resources for further reading:
// - register decoder: https://intel.github.io/SDM/definition/Read_GPR.html


use "src/tmpl.yap"


seq IR::Op
{
    // reference exists to this node. noop.
    // address of node in binary will be written
    // to arg field by assembly pass.
    REF, 

    LOAD_INT,    // mov rax, arg
    LOAD_STRING, // mov rax, "arg"
    LOAD_STATIC, // mov rax, sizeof(arg)

    PUSH,     // push rax
    POP,      // pop  rax
    PUSH_AUX, // push rbx
    POP_AUX,  // pop  rbx

    LOAD_PARAM,  // mov rax, ABI[arg]
    STORE_PARAM, // pop ABI[arg]
    
    LOAD_LOCAL,  // mov rax, [rbp - arg]
    STORE_LOCAL, // mov [rbp - arg], rax

    ENTER,  // enter arg,0
    LEAVE,  // leave; ret

    CALL, // call arg
    JMP,  // jmp arg
    JNZ,  // test rax, rax; jnz arg

    ADD, // add rax, rbx
    SUB, // sub rax, rbx
    DOT, // mov rax, [rax + rbx*8]
    DOUBLE_DOT, // lea rax, [rax + rbx*8]

    // cmp rax, rbx
    EQUAL,
    NOT_EQUAL,
    LESSER,
    GREATER,
    // movzx rax, cl

    MUL, // mul rbx
    DIV, // xor rdx, rdx; div rbx
    MOD, // xor rdx, rdx; div rbx; mov rax, rdx
    AND, // and rax, rbx
    OR,  // or  rax, rbx
    XOR, // xor rax, rbx
    SHR, // mov rcx, rbx; shr rax, cl
    SHL, // mov rcx, rbx; shl rax, cl

    INDIRECT_STORE, // mov [rax], rbx
    OFFSET_STORE,   // mov [rbx + arg], rax

    SYSCALL,
}

seq IR::Node
{
    OPCODE,
    ARG,

    // relevant node address
    //  ref            -> defintion address
    //  call / jump    -> rel32 patch address
    //  string / const -> abs64 patch address
    //  etc ...
    ADDR, 
    
    PREV, // IR::Node
    NEXT, // IR::Node
}


fn IR::Emit(ctx, opcode, arg)
{
    put new = Chunk::New(IR::Node);
    put new.IR::Node::OPCODE = opcode;
    put new.IR::Node::ARG    = arg;
    put new.IR::Node::NEXT   = Mem::NULL;

    put iter = ctx.Ctx::Global::IR_ITER;

    put iter.IR::Node::NEXT = new;
    put new.IR::Node::PREV = iter;

    put ctx.Ctx::Global::IR_ITER = new;

    return new;
}

fn IR::Addr(ctx)
{
    put output = ctx.Ctx::Global::OUTPUT;
    return Dyn::Size(output) + Tmpl::Config::LOAD_ADDR;
}


fn IR::Push8(ctx, value)
{
    put output = ctx.Ctx::Global::OUTPUT;
    Dyn::Push(output, value & 255);
}
fn IR::PushREXW(ctx)
{
    IR::Push8(ctx, 72); // 0x48
}
fn IR::Push16(ctx, value)
{
    IR::Push8(ctx, value);
    IR::Push8(ctx, value >> 8);
}
fn IR::Push32(ctx, value)
{
    IR::Push8(ctx, value);
    IR::Push8(ctx, value >> 8);
    IR::Push8(ctx, value >> 16);
    IR::Push8(ctx, value >> 24);
}
fn IR::Push64(ctx, value)
{
    put i = 0;
    lab loop;
        IR::Push8(ctx, value);
        put value = value >> 8;
        put i = i + 1;
    jump loop ~ i < 8;
}

fn IR::PatchDyn(ctx, addr, value, bytes)
{
    put vaddr = addr - Tmpl::Config::LOAD_ADDR;
    put output = ctx.Ctx::Global::OUTPUT;

    put i = 0;
    lab loop;
        put Dyn::Ptr(output).(vaddr + i) = value & 255;
        put value = value >> 8;
        put i = i + 1;
    jump loop ~ i < bytes;
}

fn IR::Patch32(ctx, addr, value)
{
    IR::PatchDyn(ctx, addr, value, 4);
}
fn IR::Patch64(ctx, addr, value)
{
    IR::PatchDyn(ctx, addr, value, 8);
}

fn IR::PushCompare(ctx, cond)
{
    // cmp rax, rbx
    IR::PushREXW(ctx);
    IR::Push8(ctx, 57);
    IR::Push8(ctx, 216);

    // setcc cl
    IR::Push8(ctx, 15); // 0xf -> setcc
    IR::Push8(ctx, cond);
    IR::Push8(ctx, 193); // 0xc1 -> cl

    // movzx rax, cl
    IR::PushREXW(ctx);
    IR::Push8(ctx, 15);  // 0xf -> movzx
    IR::Push8(ctx, 182); // 0xb6 -> movzbq
    IR::Push8(ctx, 193); // 0xc1 -> cl

}

fn IR::PostAsm(ctx, patch_nodes)
{
    put node_i = 0;
    lab loop;
        jump done ~ node_i == Dyn::Size(patch_nodes);
        put node = Dyn::Ptr(patch_nodes).node_i;
        put node_i = node_i + 1;

        put opcode = node.IR::Node::OPCODE; 
        put arg    = node.IR::Node::ARG; 
        put addr   = node.IR::Node::ADDR;

        jump op_string ~ opcode == IR::Op::LOAD_STRING;
        jump op_static ~ opcode == IR::Op::LOAD_STATIC;
        jump op_jmp    ~ opcode == IR::Op::JMP;
        jump op_jmp    ~ opcode == IR::Op::JNZ;
        jump op_call   ~ opcode == IR::Op::CALL;
        jump loop;

        lab op_string;
            put base_ptr = IR::Addr(ctx);

            put i = 0;
            lab op_string_loop;
                put char = arg.i;
                put i = i + 1;

                IR::Push64(ctx, char);
            jump op_string_loop ~ char != '\0';

            IR::Patch64(ctx, addr, base_ptr);
            jump loop;

        lab op_static;
            put base_ptr = IR::Addr(ctx);
            put i = 0;
            lab op_static_loop;
                jump op_static_done ~ i == arg;
                IR::Push8(ctx, 0);
                put i = i + 1;
                jump op_static_loop;
            lab op_static_done;

            IR::Patch64(ctx, addr, base_ptr);
            jump loop;

        lab op_jmp;
        lab op_jnz;
        lab op_call;
            // subtract 4 for the rel32 value itself.
            // 4 * 8 = 32.

            put target_addr = arg.IR::Node::ADDR;
            put offset = (target_addr - addr) - 4;
            IR::Patch32(ctx, addr, offset);
            jump loop;


        jump loop;
    lab done;
}

fn IR::Asm(ctx)
{
    // list of nodes to be patched,
    // during post-assemble
    put patch_nodes = Dyn::Create();

    Tmpl::Header(ctx);
    put iter = ctx.Ctx::Global::IR_ROOT;

lab loop;
    put iter = iter.IR::Node::NEXT;
    jump done ~ iter == Mem::NULL;
    put opcode = iter.IR::Node::OPCODE;
    put arg = iter.IR::Node::ARG;

    jump asm_ref      ~ opcode == IR::Op::REF;

    jump asm_load_int ~ opcode == IR::Op::LOAD_INT;
    jump asm_load_local  ~ opcode == IR::Op::LOAD_LOCAL;
    jump asm_store_local ~ opcode == IR::Op::STORE_LOCAL;

    jump asm_enter ~ opcode == IR::Op::ENTER;
    jump asm_leave ~ opcode == IR::Op::LEAVE;

    jump asm_push     ~ opcode == IR::Op::PUSH;
    jump asm_pop      ~ opcode == IR::Op::POP;
    jump asm_push_aux ~ opcode == IR::Op::PUSH_AUX;
    jump asm_pop_aux  ~ opcode == IR::Op::POP_AUX;

    jump asm_load_param  ~ opcode == IR::Op::LOAD_PARAM;
    jump asm_store_param ~ opcode == IR::Op::STORE_PARAM;

    jump asm_syscall ~ opcode == IR::Op::SYSCALL;

    jump asm_load_string ~ opcode == IR::Op::LOAD_STRING;
    jump asm_load_static ~ opcode == IR::Op::LOAD_STATIC;

    jump asm_indirect_store ~ opcode == IR::Op::INDIRECT_STORE;
    jump asm_offset_store   ~ opcode == IR::Op::OFFSET_STORE;

    jump asm_call ~ opcode == IR::Op::CALL;
    jump asm_jmp  ~ opcode == IR::Op::JMP;
    jump asm_jnz  ~ opcode == IR::Op::JNZ;

    jump asm_add        ~ opcode == IR::Op::ADD;
    jump asm_sub        ~ opcode == IR::Op::SUB;
    jump asm_dot        ~ opcode == IR::Op::DOT;
    jump asm_double_dot ~ opcode == IR::Op::DOUBLE_DOT;
    jump asm_equal      ~ opcode == IR::Op::EQUAL;
    jump asm_not_equal  ~ opcode == IR::Op::NOT_EQUAL;
    jump asm_lesser     ~ opcode == IR::Op::LESSER;
    jump asm_greater    ~ opcode == IR::Op::GREATER;
    jump asm_mul        ~ opcode == IR::Op::MUL;
    jump asm_div        ~ opcode == IR::Op::DIV;
    jump asm_mod        ~ opcode == IR::Op::MOD;
    jump asm_and        ~ opcode == IR::Op::AND;
    jump asm_or         ~ opcode == IR::Op::OR;
    jump asm_xor        ~ opcode == IR::Op::XOR;
    jump asm_shr        ~ opcode == IR::Op::SHR;
    jump asm_shl        ~ opcode == IR::Op::SHL;

    Error::PrintError("INTERNAL: Invalid IR opcode %d", [opcode]);

lab asm_ref;
    put iter.IR::Node::ADDR = IR::Addr(ctx);
    jump loop;    

lab asm_load_int;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 184); // B8 + 0 (0 -> rax)
    IR::Push64(ctx, arg);
    jump loop;

lab asm_load_local;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 139); // 0x8b
    IR::Push8(ctx, 133); // 0x85
    IR::Push32(ctx, (1 << 32) - arg);
    jump loop;
lab asm_store_local;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 137); // 0x89
    IR::Push8(ctx, 133); // 0x85
    IR::Push32(ctx, (1 << 32) - arg);
    jump loop;

lab asm_enter;
    IR::Push8(ctx, 200); // 0xC8 -> enter
    IR::Push16(ctx, arg); // low byte
    IR::Push8(ctx, 0);   // zero-nested
    jump loop;

lab asm_leave;
    IR::Push8(ctx, 201); // 0xC9 -> leave
    IR::Push8(ctx, 195); // 0xC3 -> near return
    jump loop;

lab asm_push;
    IR::Push8(ctx, 80); // 0x50 -> push rax
    jump loop;
lab asm_pop;
    IR::Push8(ctx, 88); // 0x58 -> pop rax
    jump loop;
lab asm_push_aux;
    IR::Push8(ctx, 83); // 0x53 -> push rbx
    jump loop;
lab asm_pop_aux;
    IR::Push8(ctx, 91); // 0x5b -> pop rbx
    jump loop;

lab asm_branch;


lab asm_call;
    IR::Push8(ctx, 232);
    put iter.IR::Node::ADDR = IR::Addr(ctx);
    IR::Push32(ctx, 0); //rel32;
    Dyn::Push(patch_nodes, iter);
    jump loop;

lab asm_jmp;
    IR::Push8(ctx, 233);
    put iter.IR::Node::ADDR = IR::Addr(ctx);
    IR::Push32(ctx, 0); //rel32
    Dyn::Push(patch_nodes, iter);
    jump loop;

lab asm_jnz;
    // test rax, rax
    IR::PushREXW(ctx);
    IR::Push8(ctx, 133);
    IR::Push8(ctx, 192);

    IR::Push8(ctx, 15);
    IR::Push8(ctx, 133);
    put iter.IR::Node::ADDR = IR::Addr(ctx);
    IR::Push32(ctx, 0); //rel32
    Dyn::Push(patch_nodes, iter);
    jump loop;
    
    
lab asm_add; IR::PushREXW(ctx); IR::Push8(ctx, 1);  IR::Push8(ctx, 216); jump loop;
lab asm_sub; IR::PushREXW(ctx); IR::Push8(ctx, 41); IR::Push8(ctx, 216); jump loop;
lab asm_dot;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 139);
    IR::Push8(ctx, 4);
    IR::Push8(ctx, 216);
    jump loop;
lab asm_double_dot;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 141);
    IR::Push8(ctx, 4);
    IR::Push8(ctx, 216);
    jump loop;

lab asm_equal;     IR::PushCompare(ctx, 148); jump loop;
lab asm_not_equal; IR::PushCompare(ctx, 149); jump loop;
lab asm_lesser;    IR::PushCompare(ctx, 146); jump loop;
lab asm_greater;   IR::PushCompare(ctx, 151); jump loop;

lab asm_mul; 
    IR::PushREXW(ctx); IR::Push8(ctx, 247); IR::Push8(ctx, 227); // mul rbx
    jump loop;
lab asm_div; 
    IR::PushREXW(ctx); IR::Push8(ctx, 49);  IR::Push8(ctx, 210); // xor rdx, rdx
    IR::PushREXW(ctx); IR::Push8(ctx, 247); IR::Push8(ctx, 243); // div rbx
    jump loop;
lab asm_mod; 
    IR::PushREXW(ctx); IR::Push8(ctx, 49);  IR::Push8(ctx, 210); // xor rdx, rdx
    IR::PushREXW(ctx); IR::Push8(ctx, 247); IR::Push8(ctx, 243); // div rbx
    IR::PushREXW(ctx); IR::Push8(ctx, 137); IR::Push8(ctx, 208); // mov rax, rdx
    jump loop;

lab asm_and; IR::PushREXW(ctx); IR::Push8(ctx, 33); IR::Push8(ctx, 216); jump loop;
lab asm_or ; IR::PushREXW(ctx); IR::Push8(ctx,  9); IR::Push8(ctx, 216); jump loop;
lab asm_xor; IR::PushREXW(ctx); IR::Push8(ctx, 49); IR::Push8(ctx, 216); jump loop;

lab asm_shr; 
    IR::PushREXW(ctx); IR::Push8(ctx, 137); IR::Push8(ctx, 217); // mov rcx, rbx
    IR::PushREXW(ctx); IR::Push8(ctx, 211); IR::Push8(ctx, 232); // shr rax, cl
    jump loop;
lab asm_shl;
    IR::PushREXW(ctx); IR::Push8(ctx, 137); IR::Push8(ctx, 217); // mov rcx, rbx
    IR::PushREXW(ctx); IR::Push8(ctx, 211); IR::Push8(ctx, 224); // shl rax, cl
    jump loop;

lab asm_load_param;
    jump loop               ~ arg == 0; // mov rax, rax -> noop
    jump asm_load_param_rdi ~ arg == 1;
    jump asm_load_param_rsi ~ arg == 2;
    jump asm_load_param_rdx ~ arg == 3;
    jump asm_load_param_r10 ~ arg == 4;
    jump asm_load_param_r8 ~ arg  == 5;
    jump asm_load_param_r9 ~ arg  == 6;
    Error::Error("PANIC: INTERNAL ERROR, DO NOT RUN EXECUTABLE");

    lab asm_load_param_rdi; IR::Push8(ctx, 72); IR::Push8(ctx, 137); IR::Push8(ctx, 248); jump loop;
    lab asm_load_param_rsi; IR::Push8(ctx, 72); IR::Push8(ctx, 137); IR::Push8(ctx, 240); jump loop;
    lab asm_load_param_rdx; IR::Push8(ctx, 72); IR::Push8(ctx, 137); IR::Push8(ctx, 208); jump loop;
    lab asm_load_param_r10; IR::Push8(ctx, 76); IR::Push8(ctx, 137); IR::Push8(ctx, 208); jump loop;
    lab asm_load_param_r8 ; IR::Push8(ctx, 76); IR::Push8(ctx, 137); IR::Push8(ctx, 192); jump loop;
    lab asm_load_param_r9 ; IR::Push8(ctx, 76); IR::Push8(ctx, 137); IR::Push8(ctx, 200); jump loop;

lab asm_store_param;
    jump asm_store_param_rax ~ arg == 0;
    jump asm_store_param_rdi ~ arg == 1;
    jump asm_store_param_rsi ~ arg == 2;
    jump asm_store_param_rdx ~ arg == 3;

    IR::Push8(ctx, 65);
    jump asm_store_param_r10 ~ arg == 4;
    jump asm_store_param_r8 ~ arg  == 5;
    jump asm_store_param_r9 ~ arg  == 6;
    Error::Error("PANIC: INTERNAL ERROR, DO NOT RUN EXECUTABLE");

    lab asm_store_param_rax; IR::Push8(ctx, 88); jump loop;
    lab asm_store_param_rdi; IR::Push8(ctx, 95); jump loop;
    lab asm_store_param_rsi; IR::Push8(ctx, 94); jump loop;
    lab asm_store_param_rdx; IR::Push8(ctx, 90); jump loop;

    lab asm_store_param_r10; IR::Push8(ctx, 90); jump loop;
    lab asm_store_param_r8 ; IR::Push8(ctx, 88); jump loop;
    lab asm_store_param_r9 ; IR::Push8(ctx, 89); jump loop;

lab asm_indirect_store;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 137);
    IR::Push8(ctx, 24);
    jump loop;
lab asm_offset_store;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 137); // mov m, r
    IR::Push8(ctx, 131); // ModR/M 0x83 -> [EBX + disp32]
    IR::Push32(ctx, arg);
    jump loop;

lab asm_syscall;
    IR::Push8(ctx, 15);
    IR::Push8(ctx, 5);
    jump loop;

lab asm_load_string;
lab asm_load_static;
    IR::PushREXW(ctx);
    IR::Push8(ctx, 184);
    put iter.IR::Node::ADDR = IR::Addr(ctx);
    IR::Push64(ctx, 0);

    Dyn::Push(patch_nodes, iter);
    jump loop;

    

lab done;
    IR::PostAsm(ctx, patch_nodes);
    Dyn::Delete(patch_nodes);
    Tmpl::Finalize(ctx);
}



